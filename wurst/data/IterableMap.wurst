package IterableMap 
import LinkedList
import HashMap

import public TypeCasting
import public Table

/** Generic Table Wrapper */
public class IterableMap<K,V> extends HashMap<K, V>
 	private let keys = new LinkedList<K>()

 	function iterator() returns LLIterator<K>
 	 	return keys.iterator()

 	function staticItr() returns LLStaticIterator<K>
 	 	return keys.staticItr()

	override function has(K key) returns bool
		return keys.contains(key)

	function clone() returns IterableMap<K, V>
		let map = new IterableMap<K, V>
		for key in this
			map.put(key, get(key))
		return map

 	/** Saves the given value under the given key */
	override function put(K key, V value)
 	 	super.put(key, value)
		if not keys.contains(key)
 	 	 	keys.add(key)
	
	/** Removes the value saved under the given key */
	override function remove(K key)
		super.remove(key)
 	 	if keys.contains(key)
 	 	 	keys.remove(key)

	function getKeys() returns LinkedList<K>
		return keys

 	ondestroy
 	 	destroy keys